---
date: 2015-01-31T08:24:26+09:00
description: 昔作ったMojolicious::Liteのサンプルを発見したので、振り返りながら解説を試みます
draft: false
image: /favicon.png
iso8601: 2015-01-31T08:24:26+09:00
tags:
  - mojolicious
  - perl
  - perl-entrance
title: Mojolicious::Liteのプレースホルダとパラメータ

---

[@nqounet](https://twitter.com/nqounet)です。

昔作ったMojolicious::Liteのサンプルを発見したので、振り返りながら解説を試みます。

第1回はプレースホルダとパラメータです。

# サンプルコード全体

`Mojolicious`のバージョンは5.75で確認しています。

ファイル名はなんでも良いですが、ひとまず`myapp.pl`と考えてください。



```perl
#!/usr/bin/env perl
use utf8;
use Mojolicious::Lite;

get '/' => sub { shift->redirect_to('/everything/54?name=hoge'); };

get '/everything/:stuff' => [stuff => qr/\d+/] => {stuff => 23} => sub {
    shift->render('welcome');
};

app->log->level('error');
app->start;

__DATA__
@@ welcome.html.ep
Stuff is <%= $stuff %>.
クエリーnameの値は「<%= param 'name' %>」です。
```




## 解説

### Line 1 - 3



```perl
#!/usr/bin/env perl
use utf8;
use Mojolicious::Lite;
```




これらは毎回書くことになります。
`use strict;`や`use warnings;`は書かなくても`use Mojolicious::Lite`すれば自動的に有効になります。

最近（というか、バージョン3.69以降なので結構経ちます）は「use utf8;」も自動的に有効になります。
いずれにしろ、書いてあっても害はありません。

### Line 5



```perl
get '/' => sub { shift->redirect_to('/everything/54?name=hoge'); };
```




これは、URLによる処理の振り分けを指定しています。

この場合は、トップページにアクセスすると、その後に書いたコードリファレンスが実行されます。

このスクリプトを`morbo myapp.pl`で起動している場合、`http://localhost:3000`にアクセスすれば、`/everything/54?name=hoge`へリダイレクトします。

リダイレクトは、例えばログインの処理の後にメンバーのページを表示したり、POSTの処理後に元のページへ戻したりするのに使えます。

ちなみに、これは単にアドレスバーに入力するのが面倒だったので書いたものです。

### Line 7 - 9



```perl
get '/everything/:stuff' => [stuff => qr/\d+/] => {stuff => 23} => sub {
    shift->render('welcome');
};
```




ここがこのサンプルの肝です。

`/everything/:stuff`の`:stuff`は、プレースホルダと言って、この部分に含まれた文字はあとで取り出すことができます。

このサンプルでは、`:stuff`に対して色々と条件をつけています。

配列のリファレンス（今の場合は`[stuff => qr/\d+/]`）を渡すと、有効にする値を指定することができます。指定できる値は正規表現（使えない機能もあります）、または配列のリファレンスで指定することができます。

ハッシュのリファレンス（今の場合は`{stuff => 23}`）を渡すと、初期値を指定することができます。初期値を指定すると、プレースホルダ部分がない場合でもマッチするようになります。

URLのパスで考えると、`/everything/98`や`/everything`、`/everything/`がマッチします。

`/everything/ab`や`/everything/3g`はマッチしません。

コードリファレンスの中身ですが、これは後で出てくる`welcome.html.ep`をテンプレートにしてレンダリングするようになっています。

### Line 11 - 12



```perl
app->log->level('error');
app->start;
```




最初の行は、ログレベルを変更しています。ログレベルには`debug`,`info`,`warn`,`error`,`fatal`の5段階があり、今のように`error`を指定すると`error`と`fatal`以外が表示（または記録）されないようになります。

`Mojolicious::Lite`は、自動的にデバッグしやすい程度にログを出してくれますが、ここではそれを表示しないようにレベルを変更しています。

本文の最後には`app->start;`を書きます。

定義してきたアプリケーションをこの文で起動させます。

### Line 14 - 17



```perl
__DATA__
@@ welcome.html.ep
Stuff is <%= $stuff %>.
クエリーnameの値は「<%= param 'name' %>」です。
```




`__DATA__`以降はテンプレートなどを書くことができます。

`@@ welcome.html.ep`というのは、次に`@@`が出てくるまでの範囲を`welcome.html.ep`というファイルとして扱います。

`render`で呼び出しされた後、このテンプレート内では、`:stuff`に入った値が`$stuff`として使用できます。

また、`param`という命令で、パラメータから値を取得できます。

この場合は、パラメータの`name`で取得できる値を直接表示します。

## 多機能だけど必要なところだけ使えばOK

`Mojolicious`を使うと、とても簡単にURLに対応する処理を書くことができます。

皆さんも美味しいところだけをうまく使ってサクッとウェブアプリを作ってみましょう。

2月28日（土）にPerl入学式の第6回としてウェブアプリを作成します。

大阪の講師は私が担当する予定です。

あなたの知らないPerlを一部お見せしますので、是非ご参加ください。

- [Perl入学式 | Perl Entrance](http://www.perl-entrance.org/)
- [Perl入学式 in大阪 第6回 Webアプリ編 on Zusaar](http://www.zusaar.com/event/12837005) ← お申し込みはこちらから。参加費は無料です。
