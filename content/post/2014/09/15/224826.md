---
date: 2014-09-15T22:48:26+09:00
description: 最近、IdobataというツールをPerl入学式のサポーターたちで使い始めました
draft: false
image: /favicon.png
iso8601: 2014-09-15T22:48:26+09:00
tags:
  - perl
  - perl-entrance
title: Perl入学式に関するツイートをIdobataに通知するボットを作った

---

[@nqounet](https://twitter.com/nqounet)です。

最近、[Idobata](https://idobata.io/)というツールをPerl入学式のサポーターたちで使い始めました。

[Idobata](https://idobata.io/)はチャットツールなのですが、webhookを経由して色々と通知を受け取れるのでなかなか快適です。

しかもなかなか充実しています。

折角なので、Perl入学式についての発言があったら通知するようなbotを作ってみました。

## だいたいこんな感じ

メインスクリプトの全体像は大体こんな感じです。



```perl
#!/usr/bin/env perl
use utf8;
use v5.12;

use AnyEvent::Twitter::Stream;
use Encode;
use Furl;

use Config::Pit;
my $config = pit_get(
    'nqounet@twitter.com',
    require => {
        consumer_key        => 'consumer_key',
        consumer_secret     => 'consumer_secret',
        token        => 'access_token',
        token_secret => 'access_token_secret',
    }
);

my $furl = Furl->new;
my $done = AE::cv;

my $listener = AnyEvent::Twitter::Stream->new(
    method   => 'filter',
    track    => 'Perl入学式',
    on_tweet => sub {
        my $tweet = shift;
        $furl->post(
            'https://idobata.io/hook/generic/xxxxxxxxxxxxxxx',
            ['Content-Type' => 'application/x-www-form-urlencoded'],
            [source         => encode_utf8(qq{$tweet->{user}{name} : $tweet->{text}})]
        );
    },
    %{$config}
);

$done->recv;
```




## まずはアプリケーションを登録

TwitterはAPIが充実していますが、使う場合は登録が必要です。

- [Create an application | Twitter Application Management](https://apps.twitter.com/app/new)

登録は英語ですが、日本語の解説を読みながらだと簡単です。

- [アプリケーションの登録 - Twitter APIの使い方](http://syncer.jp/twitter-api-create-application)

アクセストークンが必要なのでアクセストークンも発行しておきます。

発行後、しばらく待っていると発行されてページに情報が表示されるようになります。

## OAuthしましょう

各種トークンの読み込みは`Config::Pit`を使っていますが、パスワードを書かないようにしているだけですので、ハッシュで指定してもかまいません。

- [パスワード設定をコードに書かない(Config::Pit) - モダンなPerl入門](http://perl-users.jp/modules/config_pit.html)

OAuthを使用するので、`Net::OAuth`もインストールが必要です。

## Payload URL

Idobataで任意のwebhookを受信するためには、`Generic`を選択します。

設定にある`Endpoint URL`に対してデータを送信することになります。

通信には`Furl`を使用することにします。

慣れているならば`LWP::UserAgent`などでも全く問題ありません。

httpsでの通信になるので`IO::Socket::SSL`や`LWP::Protocol::https`もインストールが必要な場合があります。

## ツイートをフィルタリング

ここでは`AnyEvent::Twitter::Stream`を使うことにします。

インスタンスを生成する時に`method`に`filter`を指定し、フィルタリングする内容を`track`に指定すると、該当するツイートが流れてきた時に、`on_tweet`が実行されます。

- [POST statuses / filter | Home](https://dev.twitter.com/streaming/reference/post/statuses/filter)

また、Twitterでは、HTTPSでの接続を推奨しているため、`AnyEvent::Twitter::Stream`のドキュメントにあるとおり`Net::SSLeay`もcpanfileに書いてインストールするようにしておきましょう。

## サーバーに常駐させる

このまま実行するとターミナルを開けたままにしておかないといけないので、デーモン化してサーバーに常駐させます。

デーモン化するのは`App::Daemon`がとてもお手軽です。



```
use App::Daemon qw(daemonize);
daemonize();
```




これが書いてあるスクリプトで`start`,`stop`,`status`などのコマンドが使えるようになります。

また、デーモン化しないためには`-X`というオプションをつけるとデーモン化しないので動作確認ができます。

また、不意のエラーから復活させるためProcletで監視させることにしたので、デーモン化するのはProcletの方です。

つまり、デーモン化したProcletによってロボットを監視する構成です。

また、Procletでloggerを指定することで、プログラム本体はログの出力を意識する必要がなくなりますので、非常に便利です。

サーバーに常駐させるスクリプトを作るときには、この構成が手軽で良さそうな気がします。



```perl
#!/usr/bin/env perl
use utf8;
use v5.12;

use Proclet;
use Path::Tiny qw(path cwd);
use File::RotateLogs;

use App::Daemon qw(daemonize);
daemonize();

my $logfile = join '/', cwd->absolute, 'log', 'app.log';
path($logfile)->parent->mkpath;

my $logger = File::RotateLogs->new(
    logfile      => qq{$logfile-%Y%m%d},
    linkname     => $logfile,
    rotationtime => 86400,
    maxage       => 86400 * 14,
);

my $proclet = Proclet->new(logger => sub { $logger->print(@_); });
$proclet->service(
    code => [qw(perl main.pl)],
    tag  => 'bot',
);
$proclet->run;
```




このファイルも加えた結果、cpanfileはこうなりました。

なお、デバッグ用に`Data::Printer`も追加しています。



```
requires 'perl', '5.012001';
requires 'AnyEvent::Twitter::Stream';
requires 'Furl';
requires 'Config::Pit';
requires 'Net::OAuth';
requires 'Net::SSLeay';
requires 'IO::Socket::SSL';
requires 'App::Daemon';
requires 'Proclet';
requires 'Path::Tiny';
requires 'File::RotateLogs';
requires 'Data::Printer';
```




`carton install`でインストールされたモジュールは98個になっていました。

…多いですね。

しばらくはこの構成で様子を見てみようと思います。

## 参考になる資料

- [perl - The Perl 5 language interpreter - metacpan.org](https://metacpan.org/pod/perl)
- [AnyEvent::Twitter::Stream - Receive Twitter streaming API in an event loop - metacpan.org](https://metacpan.org/pod/AnyEvent::Twitter::Stream)
- [App::Daemon - Start an Application as a Daemon - metacpan.org](https://metacpan.org/pod/App::Daemon)
- [Config::Pit - Manage settings - metacpan.org](https://metacpan.org/pod/Config::Pit)
- [Data::Printer - colored pretty-print of Perl data structures and objects - metacpan.org](https://metacpan.org/pod/Data::Printer)
- [Furl - Lightning-fast URL fetcher - metacpan.org](https://metacpan.org/pod/Furl)
- [IO::Socket::SSL - metacpan.org](https://metacpan.org/pod/IO::Socket::SSL)
- [Net::OAuth - OAuth 1.0 for Perl - metacpan.org](https://metacpan.org/pod/Net::OAuth)
- [Net::SSLeay - metacpan.org](https://metacpan.org/pod/Net::SSLeay)
- [Proclet - minimalistic Supervisor - metacpan.org](https://metacpan.org/pod/Proclet)
